Package appel.ch03;

Helpers
	any = [0..0xffff];
	eol = (10 | 13 | 10 13);
	letter = [['a'..'z']+['A'..'Z']];
	alpha = letter | '_';
	digit = ['0'..'9'];
	alphanum = alpha | digit;
	nonzero = ['1'..'9'] digit*;
	star = '*';
	nonstar = [any - star];
	nonquote = [any - '"'];
	exponent = ('e' | 'E') ('+' | '-')? nonzero?;
	escapedchar = '\\' | '\' ''' | '\"' | '\t' | '\n';

Tokens
	// Symbols
	comma = ','; colon = ':'; semi = ';'; bang = '!'; dot = '.';
	lparen = '('; rparen = ')'; lbrack = '['; rbrack = ']'; lbrace = '{'; rbrace = '}';
	plus = '+'; minus = '-'; times = '*'; divide = '/'; mod = '%';
	pluseq = '+='; minuseq = '-='; timeseq = '*='; divideeq = '/='; modeq = '%=';
	eqeq = '=='; neq = '!='; lt = '<'; le = '<='; gt = '>'; ge = '>=';
	and = '&&'; or = '||'; eq = '=';
	inc = '++'; dec = '--';

	// Reserved
	println = 'System.out.println'; length = 'length';
	if = 'if'; else = 'else';
	for = 'for'; do = 'do'; while = 'while'; break = 'break';
	new = 'new'; throw = 'throw'; try = 'try'; catch = 'catch'; finally = 'finally';
	classkeyword = 'class'; interface = 'interface'; extends = 'extends'; implements = 'implements';
	this = 'this'; instanceof = 'instanceof';
	return = 'return'; true = 'true'; false = 'false'; null = 'null';
	void = 'void'; int = 'int'; boolean = 'boolean'; stringtype = 'String';

	// Numbers
	decimal = '0' | nonzero;
	octal = '0' ['0'..'7']*;
	hex = '0x' [digit + [['a'..'f'] + ['A'..'F']]]+;
	float = 
		(digit+ '.' digit* exponent? ) | 
		(digit* '.' digit+ exponent? ) |
		(digit+ exponent);
	
	// Strings
	char = ''' (escapedchar | any) ''';
	string = '"' nonquote* (escapedchar+ nonquote*)* '"';	
	
	// Identifier
	id = alpha alphanum*;

	// Whitespace/comments
	whitespace = (' ' | '\t' | eol)+;
	comment = 
		('//' [any - [10 + 13]]* eol?) |
		('/*' nonstar* (star+ [any - '/'] nonstar*)* star* '*/');

Ignored Tokens
	whitespace,
	comment;

Productions
	// Top-level program (one .java file)
	prog = class_or_interface_def+;
	class_or_interface_def = 
		{class} class_def |
		{interface} interface_def;
	
	// Classes and interfaces
	interface_def = interface id lbrace member_decl* rbrace;
	class_def = classkeyword id lbrace rbrace;
	
	// Definitions and declarations
	member_decl = type_spec direct_decl lparen opt_var_decl_list rparen semi;
	var_decl = type_spec direct_decl;
	primitive_type = 
		{void} void |
		{int} int |
		{bool} boolean |
		{string} stringtype;
	type_spec = 
		{primitive} primitive_type |
		{object} id |
		{array} type_spec lbrack rbrack;
	direct_decl = 
		id |
		{array} direct_decl lbrack rbrack |
		{fun} direct_decl lparen opt_var_decl_list rparen;
	opt_var_decl_list =
		|
		{decl_list} var_decl_list; 
	var_decl_list = 
		var_decl |
		{multi} var_decl_list comma var_decl; 
